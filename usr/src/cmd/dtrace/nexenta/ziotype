#!/usr/sbin/dtrace -s

/*
 * This file and its contents are supplied under the terms of the
 * Common Development and Distribution License ("CDDL"), version 1.0.
 * You may only use this file in accordance with the terms of version
 * 1.0 of the CDDL.
 *
 * A full copy of the text of the CDDL should have accompanied this
 * source.  A copy of the CDDL is also available via the Internet at
 * http://www.illumos.org/license/CDDL.
 */

/*
 * Copyright 2018, Nexenta Systems, Inc. All rights reserved.
 */

#pragma D option quiet

dtrace:::BEGIN
{
  /* see /usr/include/sys/fs/zfs.h */
  ziotype[0] = "null";
  ziotype[1] = "read";
  ziotype[2] = "write";
  ziotype[3] = "free";
  ziotype[4] = "claim";
  ziotype[5] = "ioctl";
  trace("Tracing ZIO...  Output interval 5 seconds, or Ctrl-C.\n");
}

fbt::zio_create:return
/args[1]->io_type//* skip null */
{
  @[stringof(args[1]->io_spa->spa_name),
        ziotype[args[1]->io_type] != NULL ?
    ziotype[args[1]->io_type] : "?"] = count();
}

profile:::tick-5sec,
  dtrace:::END
{
  printf("\n %-32s %-10s %10s\n", "POOL", "ZIO_TYPE", "CREATED");
  printa(" %-32s %-10s %@10d\n", @);
  trunc(@);
}
