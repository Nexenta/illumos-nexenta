#!/usr/sbin/dtrace -s

/*
 * This file and its contents are supplied under the terms of the
 * Common Development and Distribution License ("CDDL"), version 1.0.
 * You may only use this file in accordance with the terms of version
 * 1.0 of the CDDL.
 *
 * A full copy of the text of the CDDL should have accompanied this
 * source.  A copy of the CDDL is also available via the Internet at
 * http://www.illumos.org/license/CDDL.
 */

/*
 * Copyright 2018, Nexenta Systems, Inc. All rights reserved.
 */

#pragma D option quiet

dtrace:::BEGIN
{
        trace("Tracing... Hit Ctrl-C to end.\n");
}

nfsv3:::op-read-done
{
        @readbytes[args[1]->noi_curpath] = sum(args[2]->res_u.ok.data.data_len);
        @readiops[args[1]->noi_curpath] = count();
        @readbs[args[1]->noi_curpath] = avg(args[2]->res_u.ok.data.data_len);
}

nfsv4:::op-read-done
{
        @readbytes[args[1]->noi_curpath] = sum(args[2]->data_len);
        @readiops[args[1]->noi_curpath] = count();
        @readbs[args[1]->noi_curpath] = avg(args[2]->data_len);
}

nfsv3:::op-write-done
{
        @writebytes[args[1]->noi_curpath] = sum(args[2]->res_u.ok.count);
        @writeiops[args[1]->noi_curpath] = count();
        @writebs[args[1]->noi_curpath] = avg(args[2]->res_u.ok.count);
}

nfsv4:::op-write-done
{
        @writebytes[args[1]->noi_curpath] = sum(args[2]->count);
        @writeiops[args[1]->noi_curpath] = count();
        @writebs[args[1]->noi_curpath] = avg(args[2]->count);
}

tick-30sec
{
	printf("\n%12s %12s %12s %12s %12s %12s %s\n", "Rbytes", "Rops", "Rbs", "Wbytes", "WOps", "Wbs", "Pathname");
        printa("%@12d %@12d %@12d %@12d %@12d %@12d %s\n", @readbytes, @readiops, @readbs, @writebytes, @writeiops, @writebs);
	exit(0);
}

dtrace:::END
{
}
